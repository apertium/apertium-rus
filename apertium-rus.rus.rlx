DELIMITERS = "<.>" "<..>" "<...>" "<!>" "<?>" "<¶>" ; 
SOFT-DELIMITERS = "<,>" ;

LIST BOS = (>>>) ; 
LIST EOS = (<<<) ; 

LIST N = n ; 
LIST Prop = np prop ; 
LIST Pr = pr ; 
LIST Pron = prn ;
LIST Det = det ; 
LIST Num = num "<[0-9]+>"r; 
LIST A = adj ; 
LIST Adv = adv ; 
LIST CS = cnjsub ; 
LIST Interj = ij ;
LIST CC = cnjcoo ; 
LIST V = vblex vbser vbmod ; 
LIST Pred = pred ; 

LIST Cop = ("быть" vblex) ;

LIST Guio = guio "-" ; 
LIST Cm = cm ; 
LIST Excl = ("!") ;
LIST Ques = ("?") ;

LIST Percent = ("<[0-9]+%>"r) percent ; 
LIST Roman = roman ("<M{0,4}(CM|CD|D(C{0,3}))?(XC|XL|L(X{0,3}))?(IX|IV|V(I{0,3}))?>"ri num) ("<[MCDXLIVМСХ]+>"ri num) ;

LIST Pprep = pprep ; 
LIST Pers = pers ;
LIST Paren = paren ; 

LIST Parens = lpar rpar par ;
LIST Apos = apos ; 
LIST Quot = lquot rquot quot ;

LIST Short = short (adj pred) ; # pred
LIST Cmp = cmp ; 

LIST TV = tv ; 
LIST IV = iv ; 

LIST Ant = ant ; 
LIST Cog = cog ; 

LIST Msc = m ; 
LIST Fem = f ; 
LIST Neu = nt ; 

LIST Sg = sg ; 
LIST Pl = pl ;

LIST Ord = ord ; 
LIST Interr = itg ; 
LIST Rel = rel ; 

LIST DetIndecl = ("её" det pos) ("его" det pos") ("их" det pos) ;

LIST Nom = nom ; 
LIST Gen = gen ; 
LIST Dat = dat ;
LIST Acc = acc ;
LIST Ins = ins ; 
LIST Prp = prp ; 

LIST Loc = loc ; 
LIST Par = par ; 

LIST Anim = aa an ; 
LIST Inanim = nn an ; 

LIST Inf = inf ; 
LIST Imper = imp ; 
LIST Fin = past pres fut ; 

LIST Prn/Tebe = ("<тебе>"i prn) ;
LIST Prn/Komu = ("<кому>"i prn) ;
LIST Prn/Tot = ("тот"i prn) ;

LIST CS/Čto = ("что²" cnjsub)  ;

LIST Pr/V = ("в" pr) ;
LIST Pr/Na = ("на" pr) ;
LIST Pr/Nad = ("над" pr) ;
LIST Pr/Za = ("за" pr) ;
LIST Pr/Po = ("по" pr) ;
LIST Pr/S = ("с" pr) ;
LIST Pr/Dlja = ("для" pr) ; 
LIST Pr/Ot = ("от" pr) ;
LIST Pr/Do = ("до" pr) ;
LIST Pr/Iz = ("из" pr) ;
LIST Pr/Pod = ("<под>"i pr) ;
LIST Pr/Meždu = ("между" pr) ;
LIST Pr/Iz-za = ("из-за" pr) ;
LIST Pr/K = ("к" pr) ;
LIST Pr/Protiv = ("против" pr) ;
LIST Pr/Krome = ("кроме" pr) ;
LIST Pr/O = ("о" pr) ;
LIST Pr/Posle = ("после" pr) ;
LIST Pr/Nado = ("<надо>"i pr) ;
LIST Pr/Pered = ("перед" pr) ;
LIST Pr/Čerez = ("через" pr) ;
LIST Pr/Blagodarja = ("благодаря" pr) ;

LIST N/Bukva = ("буква" n) ;
LIST N/Goda = ("<года>"i n) ;
LIST N/Potom = ("<потом>"i n) ;
LIST N/Krov = ("кровь" n) ;
LIST N/Tomu = ("<тому>"i n) ;
LIST N/Golova = ("голова" n) ;
LIST N/Denga = ("деньга" n) ;
LIST N/Voda = ("<вода>"i n) ; 
LIST N/Vode = ("<водe>"i n) ; 
LIST N/Beregu = ("<берегу>"i n) ;
LIST N/Vek = ("век¹" n);
LIST N/Veko = ("веко" n) ;
LIST N/Vtora = ("<второй>"i n); 
LIST N/Mat = ("мать" n) ;
LIST N/Mater = ("матерь" n) ;
LIST N/Pomoč = ("помочь" n) ;
LIST N/Polku = ("<полку>"i n); 

LIST A/Malo = ("<мало>" adj) ;
LIST A/Božij = ("божий" adj) ;

LIST Adv/I = ("и" adv) ; 
LIST Adv/Ešë = ("ещё" adv) ; 

LIST V/Oblivatsja = ("обливаться" vblex) ;
LIST V/Pahnut = ("пахнуть" vblex) ;
LIST V/Isteč = ("истечь" vblex) ;
LIST V/Vypuskat = ("выпускать" vblex) ;
LIST V/Takaja = ("<такая>" vblex) ; 

LIST Ij/Na = ("на" ij) ;
LIST Ij/Ih = ("их²" ij) ;
LIST Ij/I = ("и¹" ij) ;


LIST O = "о" ; 
LIST Ja = "я" ; 
LIST I = "и" ;
LIST No = "но" ; 
LIST Samyj = "самый" ; 
LIST Posle = "после" ;
LIST Čto = "что" ;
LIST Nazad = "назад" ;
LIST Sliškom = "слишком" ; 
LIST Našej = "<нашей>" ; 

LIST PrefixNado = "<[бвгджзйклмнпрстуфхцчшщъь][бвгджзйклмнпрстуфхцчшщъь].*>"r ; 

LIST NumNotNom = "<([0-9])*[023456789]>"r "<([0-9])*11>"r ;

LIST LesserNum = "полтора" "два" "три" "четыре" "обо" "<([2-90]+)*[1-4]>"r ;

LIST Poltora = "полтора" ; 
LIST Pjat = "пять" "5" ; 
LIST Sto = "сто" "100" ; 
LIST Tysjača = "тысяча" "1000" ; 

LIST Day = "день" ; 
LIST Month = "месяц" ;
LIST Months = "январь" "февраль" "март" "апрель" "май" "июнь" "июль" "август" "сентябрь" "октябрь" "ноябрь" "декабрь" ; 
LIST Year = "год" "год¹" ("г." abbr) ;
LIST Century = "век" ; 
LIST Season = "сезон" ; 
LIST Seasons = "весна" "лето" "осень" "зима" ;
LIST TimeOfDay = "день" "вечер" "утро" "ночь" ; 
LIST Polovina = "половина" ;
LIST CardPoint = "западный" "южный" "юго-западный" ;
LIST TimePart = "начало" "конец" "течение" "середина" "половина" ; 

LIST Letters = ("же" n) ("у²" n) ("о²" n) ("и²" n) ("а²" n) ; # etc.

LIST InsAdv = "часом" "потом" ; 

LIST Title = ("[А-ЯA-Z][а-яa-z]+"r) ;
LIST Majusc = ("[А-ЯA-Z]+"r) ;
LIST Minusc = ("[а-яa-z]+"r) ;

LIST Prob = prb ;

LIST Facul = fac ;

SET Sem/Time = Month | Months | Year | Century | Season | Seasons | TimeOfDay | ("период") ; 

SET Sem/Distance = ("километр") | ("метр") ; 

SET Sem/Quantity = ("дюжина") ;

SET Sem/Weight = ("килограмм") | ("грамм") ;

SET Sem/Unit = Sem/Time | Sem/Distance | Sem/Weight | Sem/Quantity ; 

SET MscFemNeu = Msc | Fem | Neu ; 

SET Prep/Ins = Pr/S | Pr/Nad | Pr/Pod | Pr/Meždu | Pr/Pered ; 

SET Prep/Acc = Pr/V | Pr/Po ; # complete

SET MFN = Msc | Fem | Neu ; 

SET NGDAIP = Nom | Gen | Dat | Acc | Ins | Prp | Loc ;  

SET SentNotExcl = (".") | ("...") | ("?") ; 

SET Punct = Guio | Cm | Excl | Ques ;

SET CLB = Rel | CS ;

SECTION 

## Non-sentences 

SELECT Nom IF (-1 BOS) (0C N) (1 EOS) ; 

## Safe operations at sentence boundaries

REMOVE Prp IF (-1 BOS) ;

REMOVE Pr IF (1 EOS) ;

REMOVE Det IF (1 EOS) ;

## Safe lexicalised operations

SELECT Adv IF (0 I) (-1 No) ; 

SELECT A + $$NGDAIP IF (-1C Samyj + $$NGDAIP) ;

## Safe operations around punctuation

REMOVE Pr IF (1C Cm) ;

SELECT Posle IF (-1C Cm) ;

REMOVE Interj IF (1 SentNotExcl) ; 
  # Мне случилось говорить с одним из наших писателей о комизме в жизни, о трудности определить явление, назвать его настоящим словом.

## Safe operations with preposition/adverb ambiguities

REMOVE Pr IF (0C Pr OR Adv) (1C Nom) ;

REMOVE Pr IF (1C Fin) ; 

REMOVE Pr IF (-1C Pr) ; 

REMOVE Pr IF (1C Pr) ; 

## Safe operations after prepositions

REMOVE Nom IF (-1C Pr) ;

REMOVE NGDAIP - Gen IF (-1C* Pr/Dlja OR Pr/Do OR Pr/Ot OR Pr/Iz OR Pr/Iz-za OR Pr/Krome OR Pr/Protiv OR Pr/Posle CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Gen - Ins - Acc IF (-1C* Pr/S CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Acc - Prp - Loc IF (-1C* Pr/V OR Pr/Na CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Acc - Dat - Prp IF (-1C* Pr/Po CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Acc - Ins IF (-1C* Pr/Za CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Gen - Ins IF (-1C* Pr/Meždu CBARRIER (*) - Adv - DetIndecl) ; 

REMOVE NGDAIP - Dat IF (-1C* Pr/K CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Prp IF (-1C* Pr/O CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Ins IF (-1C* Pr/Nad CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Acc IF (-1C* Pr/Čerez CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Dat IF (-1C* Pr/Blagodarja CBARRIER (*) - Adv - DetIndecl) ;

SELECT A + $$NGDAIP IF (0C A) (-1C A + $$NGDAIP LINK -1C* Pr BARRIER (*) - A - Adv - Det) ; 

REMOVE Pers IF (0C Pers OR Det) (NOT 0 Pprep) (-1C Pr) ;
  # Так что для меня вся моя университетская наука как бы для того только и существовала под конец, чтобы ...

## Safe operations with dates 

SELECT Gen IF (0 Year) (-1 Num LINK -1 Months LINK -1 Pr/V) ;

SELECT Gen IF (0 Year) (-1 Num LINK -1 Months + Gen LINK -1 Num) ; 

SELECT Dat IF (0 Year) (-1 Num LINK -1 Pr/K) ;

REMOVE NGDAIP - Gen - Loc IF (0 Year) (-1 Num LINK -1 Pr/V) ;

SELECT Neu IF (0 Dat) (-1 Year) (1 Nazad) ; 

## Safe operations in some numeral expressions

SELECT Gen IF (-1 Percent) ;

SELECT Gen + Pl IF (-1 Sto OR Tysjača) ;

SELECT Gen + Pl IF (-1* Pjat BARRIER (*) - Adv LINK 0 Nom OR Acc);

## Safe operations with prepositional case

REMOVE Prp IF (-1C Inf)  ; 

REMOVE Prp IF (NOT -1* Pr CBARRIER Gen)  ; 

REMOVE Prp IF (-1C N) ; 

## Safe operations with determiners

REMOVE Det IF (0 Det OR Pron) (1C Fin OR CS OR Pr) ; 

REMOVE Det IF (0 Det OR Pron) (1 Cm LINK 1 CC OR CS) ; 

REMOVE Det IF (0 Det OR Pron) (1C* Fin BARRIER (*) - Adv) ; 

## Safe options with proper names 

SELECT Ant + $$NGDAIP IF (1C Cog + $$NGDAIP) ; 

SELECT Cog + $$NGDAIP IF (-1C Ant + $$NGDAIP) ; 

SECTION 

## Heuristic operations on single-character words 

SELECT Pr (0 O) (1 Prp) ; 

SELECT Pron IF (0 Ja) (-1 BOS OR CS) ; 

## Heuristic operations 

SELECT CS IF (0 Čto) (-1 Cm) (NOT 1* ("?")) ;

REMOVE Fem IF  (0C A) (0C Msc OR Fem) (1C N) (NOT 1 Fem);

REMOVE Msc IF (0C A) (0C Msc OR Neu) (1C N) (NOT 1 Msc);

REMOVE NGDAIP - Prp IF (0C A OR Det) (1C* N + Prp BARRIER NGDAIP - Prp);

REMOVE NGDAIP - Ins IF (0C A OR Det) (1C* N + Ins BARRIER NGDAIP - Ins);

REMOVE Short IF (-1C Pr) ; 

REMOVE Acc IF (-1* BOS BARRIER (*) - Nom) (0C Nom OR Acc) (NOT 1* TV) (NOT -1 Num LINK 1 Sem/Time OR Sem/Distance) (NOT 0 Sem/Time) (NOT 0 Sem/Distance) ; 

REMOVE Cmp IF (0 Cmp OR Adv) (NOT 1 Guio) ; # (NOT 1 Guio LINK 1 A) ; 

SELECT Cmp IF (0 Cmp OR Adv) (1 Guio LINK 1 A LINK 1 N) ; 

SELECT Adv IF (0C InsAdv) ;

REMOVE Adv IF (0C N + Ins OR Adv) (NOT 0 InsAdv) ;

REMOVE Acc IF (-1C Pr/S) (NOT 0 Sem/Unit) ; 

REMOVE Letters IF (NOT -1 Quot OR Parens OR Apos) (NOT 1 Quot OR Parens OR Apos) (NOT -1 N/Bukva) (NOT -1 Pr); 

REMOVE Nom IF (-1C NumNotNom) ;
  #! Для соседних с Руандой государств руандийские события апреля - июля 1994 года вылились в огромное число прибывших беженцев.

REMOVE Pl IF (0 N/Goda) (-1 LesserNum LINK NOT -1 Gen) ;

SELECT Msc IF (-1C Pr) (0 LesserNum) (1 N/Goda) ;

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Seasons OR Months);

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Seasons OR Months LINK -1 Pr); 

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Seasons OR Months LINK -1 Guio LINK -1 Seasons OR Months LINK -1 Pr); 

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Months LINK -1 Num);

SELECT Gen IF (0 N/Goda) (-1C Num) (1C EOS) ;

SELECT Gen IF (0 N/Goda) (-1 Month OR Seasons OR Day) ; 

SELECT Gen IF (0 N/Goda) (-1 A LINK -1 Month OR Seasons OR Day) ; 

SELECT Gen IF (0 N/Goda) (-1 A LINK -1 Num LINK -1 Pr) ;

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Pr) ;

REMOVE Fem IF (0C Num) (1C N + Msc + Sg + Gen)  ; 

REMOVE Neu IF (0C Num) (1C N + Msc + Sg + Gen)  ; 

SELECT Gen IF (0 N/Goda) (-1C LesserNum) (1 Nazad) ;

## Parts: половина, часть, протяжение, прошествие, течение, (начало, конец,)

#REMOVE Pl IF (0 N/Goda) (NOT -1* Num BARRIER NGDAIP - Nom OR Acc)  

#SELECT N + $$NGDAIP IF (0 A OR N) (-1C A + $$NGDAIP LINK -1* Pr CBARRIER (*) - $$NGDAIP) (1C N) (NOT 1C N + $$NGDAIP) ; 
  # По официальным данным правительства Руанды, число погибших составило 937 тысяч человек.
  #! Мать Кардуччи, умная и образованная женщина, противница всякого ханженства, внушила ему в ранние детские года горячий патриотизм и ненависть к немцам, и по трагедиям Альфьери и патриотическим одам Верни учила его азбуке.

REMOVE Pr/Nad IF (0 Pr/Nado) (NOT 1 PrefixNado) ; 

REMOVE Pr/Nad IF (1C Inf) ;

SELECT N/Potom IF (-1 V/Oblivatsja OR V/Pahnut OR V/Isteč) ;

SELECT N/Potom IF (1 CC LINK 1 N/Krov) ;

REMOVE N/Potom IF (NOT -1 Prep/Ins OR A + Ins); 

REMOVE N/Tomu IF (NOT -1* Pr/Po LINK -1* V/Vypuskat) ;

REMOVE Msc IF (0C N/Golova) (0C Msc OR Fem) ;

REMOVE A/Malo IF (1 Gen) ; 

REMOVE A/Malo IF (-1 Sliškom) ; 

REMOVE Imper IF (0 Našej) (NOT 1* Excl) ;

REMOVE Interj IF (0 Ij/Na) (NOT 1* Excl) (NOT 1 Prn/Tebe) (NOT 1 Cm) ;

SELECT Loc IF (0 N/Beregu) (-1* Pr/Na CBARRIER (*) - Prp) ;

REMOVE Fin IF (-1C Pr/K) (0C Fin OR Dat) ;

REMOVE Fin IF (-1C Pr/Na) (0C Fin OR Loc) ;

SELECT Ord IF (0 Num OR Ord) (1C N/Vek) ;

SELECT Paren IF (-1 Cm) (0 Paren OR Short) (1 Cm) ; 

SELECT N/Mater IF (0 N/Mat OR N/Mater) (-1 A/Božij) ;

SELECT N/Mater IF (0 N/Mat OR N/Mater) (1 A/Božij) ;

SELECT Interr IF (0 Prn/Komu) (NOT -1* Interr BARRIER Interr) (NOT 1* Interr BARRIER Interr) (1* Ques) ; 

SELECT Rel IF (0 Prn/Komu) (-1 Cm LINK -1 Prn/Tot) ;

SELECT N/Vek IF (0 N/Veko OR N/Vek) (-1 Roman LINK -1 TimePart)  ; 

SELECT N/Vek IF (0 N/Veko OR N/Vek) (-1 Roman LINK -1 Pr)  ; 

SELECT Gen IF (0 N/Vek) (0C Sg + Gen OR Pl) (-1 Roman LINK -1 TimePart) ;

SELECT Gen IF (0 N/Vek) (0C Sg + Gen OR Pl) (-1 Roman LINK -1 Guio LINK -1 Roman LINK -1 TimePart) ;

SELECT Gen IF (0 N/Vek) (0C Sg + Gen OR Pl) (-1 A + Gen LINK -1 TimePart) ;

SELECT Loc IF (-1 Pr/O) (0 N/Polku) ; 

REMOVE Loc IF (NOT -1* Pr/V OR Pr/Na) ;

###############################################################################

# после/посол

# SELECT Nom + $$MFN IF (0C Prop) (0C Acc OR Nom) (NOT -1C* Nom) (NOT 1C* Nom) (NOT -1* TV) (NOT 1* TV) (-1C* Cop + $$MFN) ;

# SELECT Nom + $$MFN IF (0C Prop) (0C Acc OR Nom) (NOT -1C* Nom) (NOT 1C* Nom) (NOT -1* TV BARRIER CLB) (NOT 1* TV BARRIER CLB) (1C* Cop + $$MFN BARRIER CLB) ;

# REMOVE Acc IF (0 Nom OR Acc) (NOT -1* TV) (NOT -1C* Pr) (NOT 1* TV) ;
# REMOVE Acc IF (NOT -1* TV) (-1* IV LINK 0 Fin) (NOT 1* TV BARRIER Fin) ;

# REMOVE CS IF (0 CS/Čto) (NOT -1* V OR Pred) ;

  # Нападающие выжигали огнем места, где могли спрятаться их жертвы: дома с труднодоступными помещениями, церкви, леса, перелески и т. п.

# Distinguishing between adj/participle: if it gets arguments, it's not an adjective.

#SECTION

#LIST @CLB = @CLB ;

#MAP @CLB TARGET Cm IF (-1C* Fin) (1C* Fin) (NOT -1* CC OR CS) (NOT 1* CC OR CS);

SECTION 

REMOVE N/Denga ; 

REMOVE Msc IF (0 N/Voda OR N/Vode) ; 

SELECT Majusc IF (0C Roman) ;

REMOVE N/Pomoč IF (0 N/Pomoč OR V) ;

REMOVE V IF (0 V/Takaja) ; 

REMOVE N IF (0 N/Vtora) ; 

REMOVE V IF (0 Pr/Dlja) ; 

REMOVE N IF (0 Pr/Pod) ;

REMOVE N/Mater IF (0 N/Mat OR N/Mater) ; 

REMOVE Ij/Ih IF (NOT 1 Excl) ; 

REMOVE Ij/I IF (NOT 1 Punct) ; 

REMOVE Adv/I IF (NOT -1 Adv/Ešë) ;

REMOVE Prob ; 

REMOVE Facul ; 

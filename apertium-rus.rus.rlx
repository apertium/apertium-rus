DELIMITERS = "<.>" "<..>" "<...>" "<!>" "<?>" "<¶>" ; 
SOFT-DELIMITERS = "<,>" ;

LIST BOS = (>>>) ; 
LIST EOS = (<<<) ; 

LIST N = n ; 
LIST Pr = pr ; 
LIST Pron = prn ;
LIST Det = det ; 
LIST Num = num ; 
LIST A = adj ; 
LIST Adv = adv ; 
LIST CS = cnjsub ; 
LIST Interj = ij ;
LIST CC = cnjcoo ; 
LIST V = vblex vbser vbmod ; 
LIST Guio = guio ; 

LIST Percent = ("<[0-9]+%>"r) percent ; 
LIST Roman = roman ; 
LIST Pprep = pprep ; 
LIST Pers = pers ;

LIST Short = short ; # pred
LIST Cmp = cmp ; 

LIST TV = tv ; 
LIST IV = iv ; 

LIST Ant = ant ; 
LIST Cog = cog ; 

LIST Masc = m ; 
LIST Fem = f ; 
LIST Neut = nt ; 

LIST Sg = sg ; 
LIST Pl = pl ;

LIST DetIndecl = ("её" det pos) ("его" det pos") ("их" det pos) ;

LIST Nom = nom ; 
LIST Gen = gen ; 
LIST Dat = dat ;
LIST Acc = acc ;
LIST Ins = ins ; 
LIST Prp = prp ; 

LIST Loc = loc ; 
LIST Par = par ; 

LIST Anim = aa an ; 
LIST Inanim = nn an ; 

LIST Inf = inf ; 

LIST Fin = past pres fut ; 

LIST Pr/V = ("в" pr) ;
LIST Pr/Na = ("на" pr) ;
LIST Pr/Nad = ("над" pr) ;
LIST Pr/Za = ("за" pr) ;
LIST Pr/Po = ("по" pr) ;
LIST Pr/S = ("с" pr) ;
LIST Pr/Dlja = ("для" pr) ; 
LIST Pr/Ot = ("от" pr) ;
LIST Pr/Do = ("до" pr) ;
LIST Pr/Iz = ("из" pr) ;
LIST Pr/Meždu = ("между" pr) ;
LIST Pr/Iz-za = ("из-за" pr) ;
LIST Pr/K = ("к" pr) ;
LIST Pr/Protiv = ("против" pr) ;
LIST Pr/Krome = ("кроме" pr) ;
LIST Pr/O = ("о" pr) ;

LIST Cm = cm ; 

LIST O = "о" ; 
LIST Ja = "я" ; 
LIST I = "и" ;
LIST No = "но" ; 
LIST Samyj = "самый" ; 
LIST Posle = "после" ;
LIST Čto = "что" ;
LIST Nazad = "назад" ;

LIST Pjat = "пять" "5" ; 
LIST Sto = "сто" "100" ; 
LIST Tysjača = "тысяча" "1000" ; 

LIST Month = "январь" "февраль" "апрель" "май" "июнь" "июль" "сентябрь" "октябрь" "ноябрь" "декябрь" ; 
LIST Year = "год" "год¹" ("г." abbr) ;
LIST Century = "век" ; 

LIST Majusc = ("<[А-Я]>"r) ;
LIST Minusc = ("<[а-я]>"r) ;

SET MascFemNeut = Masc | Fem | Neut ; 

SET NGDAIP = Nom | Gen | Dat | Acc | Ins | Prp ; 

SET SentNotExcl = (".") | ("...") | ("?") ; 

SECTION 

## Non-sentences 

SELECT Nom IF (-1 BOS) (0C N) (1 EOS) ; 

## Safe operations at sentence boundaries

REMOVE Prp IF (-1 BOS) ;

REMOVE Pr IF (1 EOS) ;

REMOVE Det IF (1 EOS) ;

## Safe lexicalised operations

SELECT Adv IF (0 I) (-1 No) ; 

SELECT A + $$NGDAIP IF (-1C Samyj + $$NGDAIP) ;

## Safe operations around punctuation

REMOVE Pr IF (1C Cm) ;

SELECT Posle IF (-1C Cm) ;

REMOVE Interj IF (1 SentNotExcl) ; 
  # Мне случилось говорить с одним из наших писателей о комизме в жизни, о трудности определить явление, назвать его настоящим словом.

## Safe operations with preposition/adverb ambiguities

REMOVE Pr IF (0C Pr OR Adv) (1C Nom) ;

REMOVE Pr IF (1C Fin) ; 

## Safe operations after prepositions

REMOVE Nom IF (-1C Pr) ;

REMOVE NGDAIP - Gen IF (-1C* Pr/Dlja OR Pr/Do OR Pr/Ot OR Pr/Iz OR Pr/Iz-za OR Pr/Krome OR Pr/Protiv CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Gen - Ins - Acc IF (-1C* Pr/S CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Acc - Prp IF (-1C* Pr/V OR Pr/Na CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Acc - Dat - Prp IF (-1C* Pr/Po CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Acc - Ins IF (-1C* Pr/Za CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Gen - Ins IF (-1C* Pr/Meždu CBARRIER (*) - Adv - DetIndecl) ; 

REMOVE NGDAIP - Dat IF (-1C* Pr/K CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Prp IF (-1C* Pr/O CBARRIER (*) - Adv - DetIndecl) ;

REMOVE NGDAIP - Ins IF (-1C* Pr/Nad CBARRIER (*) - Adv - DetIndecl) ;

SELECT A + $$NGDAIP IF (0C A) (-1C A + $$NGDAIP LINK -1C* Pr BARRIER (*) - A - Adv - Det) ; 

REMOVE Pers IF (0C Pers OR Det) (NOT 0 Pprep) (-1C Pr) ;
  # Так что для меня вся моя университетская наука как бы для того только и существовала под конец, чтобы ...

## Safe operations with dates 

SELECT Gen IF (0 Year) (-1 Num LINK -1 Month LINK -1 Pr/V) ;

SELECT Gen IF (0 Year) (-1 Num LINK -1 Month + Gen LINK -1 Num) ; 

SELECT Dat IF (0 Year) (-1 Num LINK -1 Pr/K) ;

REMOVE NGDAIP - Gen - Loc IF (0 Year) (-1 Num LINK -1 Pr/V) ;

SELECT Neut IF (0 Dat) (-1 Year) (1 Nazad) ; 

## Safe operations in some numeral expressions

SELECT Gen IF (-1 Percent) ;

SELECT Gen + Pl IF (-1 Sto OR Tysjača) ;

SELECT Gen + Pl IF (-1* Pjat BARRIER (*) - Adv LINK 0 Nom OR Acc);

## Safe operations with prepositional case

REMOVE Prp IF (-1C Inf)  ; 

REMOVE Prp IF (NOT -1* Pr CBARRIER Gen)  ; 

REMOVE Prp IF (-1C N) ; 

## Safe operations with determiners

REMOVE Det IF (0 Det OR Pron) (1C Fin OR CS OR Pr) ; 

REMOVE Det IF (0 Det OR Pron) (1 Cm LINK 1 CC OR CS) ; 

REMOVE Det IF (0 Det OR Pron) (1C* Fin BARRIER (*) - Adv) ; 

## Safe options with proper names 

SELECT Ant + $$NGDAIP IF (1C Cog + $$NGDAIP) ; 

SELECT Cog + $$NGDAIP IF (-1C Ant + $$NGDAIP) ; 

## Heuristic operations on single-character words 

SELECT Pr (0 O) (1 Prp) ; 

SELECT Pron IF (0 Ja) (-1 BOS OR CS) ; 

## Heuristic operations 

SELECT CS IF (0 Čto) (-1 Cm) (NOT 1* ("?")) ;

REMOVE Fem IF  (0C A) (0C Masc OR Fem) (1C N) (NOT 1 Fem);

REMOVE Masc IF (0C A) (0C Masc OR Neut) (1C N) (NOT 1 Masc);

REMOVE NGDAIP - Prp IF (0C A OR Det) (1C* N + Prp BARRIER NGDAIP - Prp);

REMOVE NGDAIP - Ins IF (0C A OR Det) (1C* N + Ins BARRIER NGDAIP - Ins);

REMOVE Short (-1C Pr) ; 

REMOVE Acc IF (-1* BOS BARRIER (*) - Nom) (0C Nom OR Acc) (NOT 1* TV) ; 

SELECT N + $$NGDAIP IF (0 A OR N) (-1C A + $$NGDAIP LINK -1* Pr CBARRIER (*) - $$NGDAIP) (1C N) (NOT 1C N + $$NGDAIP) ; 
  # По официальным данным правительства Руанды, число погибших составило 937 тысяч человек.

SELECT Cmp IF (0 Cmp OR Adv) (1 Guio LINK 1 A LINK 1 N) ; 

###############################################################################

# над/надо
# после/посол


# REMOVE Acc IF (0 Nom OR Acc) (NOT -1* TV) (NOT -1C* Pr) (NOT 1* TV) ;
# REMOVE Acc IF (NOT -1* TV) (-1* IV LINK 0 Fin) (NOT 1* TV BARRIER Fin) ;

  # Нападающие выжигали огнем места, где могли спрятаться их жертвы: дома с труднодоступными помещениями, церкви, леса, перелески и т. п.

# Distinguishing between adj/participle: if it gets arguments, it's not an adjective.
